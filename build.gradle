buildscript {
	ext {
		springBootVersion = '2.7.1'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
	}
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.onjung'
	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = '11'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	// 하위 모듈에서 공통으로 사용하는 세팅 추가
	dependencies {
		implementation 'com.h2database:h2'
		implementation 'mysql:mysql-connector-java'

		implementation 'org.springframework.boot:spring-boot-starter-web'

		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-validation'

		implementation 'org.springframework.boot:spring-boot-starter-cache'
		implementation group: 'net.sf.ehcache', name: 'ehcache', version: '2.10.9.2'

		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		implementation 'org.slf4j:slf4j-api:1.7.31'

		implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.3'

		implementation 'junit:junit:4.13.1'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	test {
		useJUnitPlatform()
		systemProperty "jasypt.encryptor.password", project.getProperties().get("jasypt.encryptor.password")
	}
}

project(':onjung-api') {
	// 공통 코드

	bootJar { enabled = false } // core 은 bootJar 로 패키징 할 필요 없음
	jar { enabled = true }

	dependencies {
	}
}

project(':onjung-core') {
	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation project(':onjung-api') // 컴파일 시 onjung-api project 로딩
		implementation 'org.springframework.boot:spring-boot-starter-security'
		testImplementation 'org.springframework.security:spring-security-test'
	}
}

project(':onjung-batch') {
	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation project(':onjung-api') // 컴파일 시 onjung-api project 로딩

		implementation 'org.springframework.boot:spring-boot-starter-batch'
		testImplementation 'org.springframework.batch:spring-batch-test'
		implementation 'org.springframework.boot:spring-boot-starter-quartz'
	}
}

project(':onjung-chatting') {
	// 공통 코드

	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation project(':onjung-api') // 컴파일 시 onjung-api project 로딩
	}
}